ext {
	/* Plugin Information */
	jdkVersion = '11'
	/* Project Information */
	projectGroup = 'com.unideb.qsa'
	projectMajorVersion = '1'
	projectMinorVersion = '0'
	projectBuildVersion = System.getenv('TRAVIS_BUILD_NUMBER') ?: '0-SNAPSHOT'
	projectVersion = projectMajorVersion + '.' + projectMinorVersion + '.' + projectBuildVersion
	/* Secrets */
	// Note: Use your own name and token
	githubOwnUsername = System.getenv('GITHUB_OWN_USERNAME') ?: 'YOUR_USERNAME'
	githubOwnToken = System.getenv('GITHUB_OWN_TOKEN') ?: 'YOUR_TOKEN'
}

group = projectGroup
version = projectVersion

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'java-library'

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
	maven {
		url 'https://jitpack.io'
	}
	maven {
		url 'https://maven.pkg.github.com/queueing-systems-assistance/qsa'
		credentials {
			username = githubOwnUsername
			password = githubOwnToken
		}
	}
}

dependencies {
	testImplementation group: 'com.unideb.qsa', name: 'qsa-config-domain', version: '1.0.2'
	testImplementation group: 'com.unideb.qsa', name: 'qsa-config-validator', version: '1.0.2'
	testImplementation group: 'com.unideb.qsa', name: 'qsa-config-assembler', version: '1.0.1'

	testImplementation group: 'org.testng', name: 'testng', version: '6.14.3'
}

sourceCompatibility = jdkVersion

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

test {
	useTestNG()
	testLogging.showStandardStreams = true
}

afterEvaluate {
	install.dependsOn(assembleConfigs)
}

task assembleConfigs(type: JavaExec) {
	classpath = sourceSets.test.runtimeClasspath
	main = "com.unideb.qsa.config.assembler.AssemblerApplication"
}