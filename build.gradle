subprojects {

  apply plugin: 'java'
  apply plugin: 'checkstyle'

  group = 'com.unideb.qsa'
  version = project.getProperties().get('release.version') ?: 'RELEASE.VERSION.UNKNOWN'

  checkstyle {
    toolVersion = libs.versions.checkstyle.get()
    showViolations = true
    ignoreFailures = false
  }

  tasks.withType((Class) JavaCompile) {
    options.encoding = 'UTF-8'
  }

  clean {
    delete "${projectDir}/src/main/.aws-sam/"
  }

  test {
    useTestNG()
    scanForTestClasses = false
    testLogging {
      afterSuite { desc, result ->
        if (!desc.parent) {
          println "Results: ${result.getResultType()} (${result.getTestCount()} tests, ${result.getSuccessfulTestCount()} successes, ${result.getFailedTestCount()} failures, ${result.getSkippedTestCount()} skipped)"
        }
      }
    }
  }
}
